QuplaSource ::= (quplaStmt)*

quplaStmt ::= ImportStmt | TypeStmt | LutStmt | FuncStmt | ExecStmt | TemplateStmt | UseStmt


ImportStmt ::= 'import' moduleName


TypeStmt ::= 'type' typeName (TritVectorDef | TritStructDef | entityAlias)

TritVectorDef ::= '[' ConstExpr ']'

TritStructDef ::= '{' (fieldName TritVectorDef)+ '}'


LutStmt ::= 'lut' lutName (('{' LutEntry+ '}') | entityAlias)

LutEntry ::= tritList '=' tritList

tritList ::= trit (',' trit)*


FuncStmt ::= 'func' funcSignature funcBody

funcSignature ::= ConstTypeName funcName ('<' ConstTypeName (',' ConstTypeName)* '>')? '(' (funcParam (',' funcParam)*) ')'

funcParam ::= ConstTypeName paramName

funcBody ::= '{' envExpr StateExpr* AssignExpr* ('return' CondExpr) '}'

envExpr ::= JoinExpr* AffectExpr*


ExecStmt ::= ('eval' | ('test' VectorExpr '=')) FuncExpr


TemplateStmt ::= 'template' templateName '<' (placeholderTypeName (',' placeholderTypeName)*) '>' (templateTypeRelation)? templateBody

templateTypeRelation ::= '=' templateTypeName ('+' templateTypeName)+

templateBody ::= '{' TypeStmt* FuncStmt+ '}'


UseStmt ::= 'use' templateName typeInstantiation (',' typeInstantiation)*

typeInstantiation ::= '<' (ConstTypeName (',' ConstTypeName)*) '>'


entityAlias ::= '=' entityName '@' moduleName


JoinExpr ::= 'join' environmentName ('limit' ConstNumber)?

AffectExpr ::= 'affect' environmentName ('delay' ConstNumber)?

StateExpr ::= 'state' ConstTypeName varName

AssignExpr ::= varName '=' CondExpr

CondExpr ::= MergeExpr ('?' MergeExpr ':' ('null' | CondExpr))?

MergeExpr ::= ConcatExpr ('|' ConcatExpr)*

ConcatExpr ::= PostfixExpr ('&' PostfixExpr)*

PostfixExpr ::= SubExpr | VectorExpr | SliceExpr | FuncExpr | LutExpr | TypeExpr

SubExpr ::= '(' CondExpr ')'

VectorExpr ::= trit | integer | float

SliceExpr ::= (varName | paramName) ('.' NameExpr)* ('[' ConstExpr (':' ConstExpr)? ']')?

FuncExpr ::= funcName ('<' ConstTypeName (',' ConstTypeName)* '>')? '(' (CondExpr (',' CondExpr)*) ')'

LutExpr ::= lutName '[' (MergeExpr (',' MergeExpr)*) ']'

TypeExpr ::= ConstTypeName '{' FieldExpr+ '}'

FieldExpr ::= fieldName '=' CondExpr


ConstExpr ::= ConstTerm ([-+] ConstTerm)*

ConstTerm ::= ConstFactor ([*/%] ConstFactor)*

ConstFactor ::= ConstSubExpr | ('-' ConstFactor) | ConstNumber | (ConstTypeName ('.' NameExpr)*)

ConstSubExpr ::= '(' ConstExpr ')'

ConstNumber ::= number

ConstTypeName ::= typeName

NameExpr ::= name


trit ::= [-01]

name ::= [a-zA-Z][0-9a-zA-Z_]*

number ::= [0-9]+

integer ::= '-'? number

float ::= '-'? number '.' number
